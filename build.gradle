plugins {
  id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

repositories {
  if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
    mavenCentral()
  }
  else {
    maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
  }
}

def dependencyVersions = [
  dropwizard:"1.3.8"
]

dependencies {
  implementation  "io.dropwizard:dropwizard-views:$dependencyVersions.dropwizard",
                  'de.neuland-bfi:jade4j:0.4.0',
                  'com.google.guava:guava:19.0',
                  'org.apache.commons:commons-lang3:3.4',
                  'org.glassfish.jersey.core:jersey-server:2.23.2'

  testImplementation  'junit:junit:4.11',
                      'org.assertj:assertj-core:1.6.0'
}

task sourceJar(type: Jar) {
  archiveClassifier = "sources"
  from sourceSets.main.allJava
}

java {
  withJavadocJar()
  withSourcesJar()
}

group 'uk.gov.ida'
version "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

nexusPublishing {
  useStaging = true
  repositories {
    sonatype {
      // because we registered in Sonatype after 24 Feb 2021, we provide these URIs
      // see: https://github.com/gradle-nexus/publish-plugin/blob/master/README.md
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
      username = System.getenv("SONATYPE_USERNAME")
      password = System.getenv("SONATYPE_PASSWORD")
    }
  }
}

signing {
  useInMemoryPgpKeys(
          System.getenv("MAVEN_CENTRAL_SIGNING_KEY"),
          System.getenv("MAVEN_CENTRAL_SIGNING_KEY_PASSWORD")
  )
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'Dropwizard Jade'
        packaging = 'jar'
        description = 'Dropwizard extension to use the Jade templating engine in Views'
        url = 'https://github.com/alphagov/dropwizard-jade'
        artifactId = 'dropwizard-jade'

        scm {
          url = 'https://github.com/alphagov/dropwizard-jade'
          connection = 'scm:git:git://github.com/alphagov/dropwizard-jade.git'
          developerConnection = 'scm:git:ssh://git@github.com:alphagov/dropwizard-jade.git'
        }

        licenses {
          license {
            name = 'MIT Licence'
            url = 'https://github.com/alphagov/dropwizard-logstash/blob/master/LICENCE'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            name = 'GDS Developers'
          }
        }
      } // pom
    }
  }
}

defaultTasks 'clean', 'publishToMavenLocal'
